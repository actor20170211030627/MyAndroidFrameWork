apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion       rootProject.ext.android.minSdkVersion
        targetSdkVersion    rootProject.ext.android.targetSdkVersion
        versionCode         rootProject.ext.android.versionCode
        versionName         rootProject.ext.android.versionName

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //以下为配置library注释在打包jar后保留:https://www.jianshu.com/p/b04ef4029b90
    // 打包源码jar
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }
    // 打包文档jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    // javadoc configuration
    javadoc {
        options{
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            links "http://docs.oracle.com/javase/7/docs/api"
//            title javadocName
        }
    }

    //Android8, 在android节点内添加
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'META-INF/*******'
    }
}

afterEvaluate {
    // 排除 BuildConfig.class 和 R.class
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
    generateReleaseResValues.enabled = false
    generateDebugResValues.enabled = false
}

dependencies {
    //implementation fileTree(dir: "libs", include: ["*.jar", '*.aar'])

    /**
     * 新建kotlin项目, 能得到合适的版本号:
     * implementation 'androidx.core:core-ktx:1.8.0'
     * implementation 'androidx.appcompat:appcompat:1.4.1'
     * implementation 'com.google.android.material:material:1.5.0'
     * implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
     */
    //https://developer.android.google.cn/jetpack/androidx/releases/appcompat?hl=zh-cn
    api rootProject.ext.dependencies.appcompat

    //https://github.com/material-components/material-components-android BottomSheetDialog等
    api (rootProject.ext.dependencies.material) {
//        exclude group: 'androidx.constraintlayout', module: 'constraintlayout' //2.0.1
    }

    //https://developer.android.google.cn/jetpack/androidx/releases/swiperefreshlayout?hl=zh-cn 下拉刷新
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    //https://github.com/androidx/constraintlayout 约束布局
    api 'androidx.constraintlayout:constraintlayout:2.1.4'


    //https://github.com/google/gson
    //不混淆解析类: -keep class com.package.xxx.bean.** { *; }
    api 'com.google.code.gson:gson:2.10.1'

    //https://github.com/bumptech/glide
    //https://github.com/bumptech/glide/blob/master/library/build.gradle
    //https://github.com/bumptech/glide/blob/master/settings.gradle     //具体版本号
    api ('com.github.bumptech.glide:glide:4.12.0') {
        exclude group: 'androidx.fragment', module: 'fragment'  //1.3.6
        exclude group: 'androidx.vectordrawable', module: 'vectordrawable-animated' //1.1.0, appcompat里面有
    }
    //annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    //https://github.com/Blankj/AndroidUtilCode 许多工具
    //https://github.com/Blankj/AndroidUtilCode/blob/master/lib/utilcode/build.gradle   依赖
    //https://github.com/Blankj/AndroidUtilCode/blob/master/buildSrc/src/main/groovy/Config.groovy
    api ('com.blankj:utilcodex:1.31.1') {
        // 以非传递性方式引入依赖，即仅引入该依赖本身，而不会引入其依赖的其他库。
        transitive = false
    }

    //https://github.com/Tencent/MMKV 腾讯 MMKV  (EasyHttp缓存需要)
    //https://github.com/Tencent/MMKV/blob/master/Android/MMKV/mmkv/build.gradle
    api ('com.tencent:mmkv-static:1.3.2') {
        transitive = false
    }

    //https://github.com/square/okhttp
    api "com.squareup.okhttp3:okhttp:4.9.3"

    //https://github.com/getActivity/EasyHttp 轮子哥网络请求框架
    api ('com.github.getActivity:EasyHttp:13.0') {
        transitive = false
    }

    //https://github.com/getActivity/ShapeView 轮子哥Shape 框架
    api ('com.github.getActivity:ShapeView:9.3') {
        //https://github.com/getActivity/ShapeView/blob/master/library/build.gradle
        exclude group: 'androidx.appcompat', module: 'appcompat'                //1.4.1
        exclude group: 'com.google.android.material', module: 'material'        //1.0.0
        exclude group: 'androidx.constraintlayout', module: 'constraintlayout'  //1.1.3
    }

    //https://github.com/getActivity/XXPermissions 轮子哥权限(Android 10需要适配分区存储特性)
    api 'com.github.getActivity:XXPermissions:20.0'

    // 吐司框架：https://github.com/getActivity/Toaster  轮子哥吐司
    api 'com.github.getActivity:Toaster:12.6'

    //https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    //TODO: 考虑使用: https://github.com/liangjingkanji/BRV
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'

    //https://github.com/li-xiaojun/XPopup 各种Dialog & Popup (compileSdkVersion 29)
    api ('com.github.li-xiaojun:XPopup:2.10.0') {
        //https://github.com/junixapp/XPopup/blob/master/library/build.gradle
        exclude group: 'androidx.appcompat', module: 'appcompat'        //1.3.1
        exclude group: 'com.google.android.material', module: 'material'//1.4.0
        exclude group: 'androidx.recyclerview', module: 'recyclerview'  //1.2.1
        exclude group: 'com.github.bumptech.glide', module: 'glide'     //4.12.0
        exclude group: 'com.github.li-xiaojun', module: 'EasyAdapter'   //1.2.8
    }
    //XPopup 依赖了 EasyAdapter
    api ('com.github.li-xiaojun:EasyAdapter:1.2.8') {
        //https://github.com/junixapp/EasyAdapter/blob/master/easy-adapter/build.gradle
        exclude group: 'androidx.appcompat', module: 'appcompat'        //1.1.0
        exclude group: 'androidx.recyclerview', module: 'recyclerview'  //1.1.0
        /**
         * 编译报错:
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.internal.jdk7.JDK7PlatformImplementations found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk7-1.5.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0)
         *
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk8-1.6.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0)
         */
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7' //1.5.0
    }

    /**
     * https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
     */
    constraints {
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.internal.jdk7.JDK7PlatformImplementations found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk7-1.5.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0)
         */
        //https://github.com/JetBrains/kotlin/tree/master/libraries/stdlib/jdk7
        //依赖版本列表: https://central.sonatype.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk7
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk8-1.6.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0)
         */
        //依赖版本列表: https://central.sonatype.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk8
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }


    //https://github.com/google/flexbox-layout
//    compileOnly 'com.google.android:flexbox:1.0.0'
    compileOnly 'com.google.android.flexbox:flexbox:3.0.0'

    //https://github.com/yellowcath/VideoProcessor 视频压缩
    compileOnly 'com.github.yellowcath:VideoProcessor:2.4.2'
}

//configurations.all {
//    resolutionStrategy {
//        //如果 constraintlayout 2.0.4版本, 会有一些依赖包的androidx_core版本冲突, 这儿强制使用2.1.4
//        force rootProject.ext.dependencies.androidx_core
//    }
//}