apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion       rootProject.ext.android.minSdkVersion
        targetSdkVersion    rootProject.ext.android.targetSdkVersion
        versionCode         rootProject.ext.android.versionCode
        versionName         rootProject.ext.android.versionName

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //以下为配置library注释在打包jar后保留:https://www.jianshu.com/p/b04ef4029b90
    // 打包源码jar
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }
    // 打包文档jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    // javadoc configuration
    javadoc {
        options{
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            links "http://docs.oracle.com/javase/7/docs/api"
//            title javadocName
        }
    }

    //Android8, 在android节点内添加
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'META-INF/*******'
    }
}

afterEvaluate {
    // 排除 BuildConfig.class 和 R.class
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
    generateReleaseResValues.enabled = false
    generateDebugResValues.enabled = false
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    //compileOnly rootProject.ext.dependencies.support_v4
    //compileOnly rootProject.ext.dependencies.appcompat_v7
    //约束布局
    //compileOnly rootProject.ext.dependencies.constraint_layout

    //BottomSheetDialog等
    api rootProject.ext.dependencies.material

    //https://developer.android.google.cn/jetpack/androidx/releases/swiperefreshlayout?hl=zh-cn
    api rootProject.ext.dependencies.swiperefreshlayout


    //https://github.com/google/gson
    //不混淆解析类: -keep class com.package.xxx.bean.** { *; }
    api 'com.google.code.gson:gson:2.10.1'

    //https://github.com/bumptech/glide
    api 'com.github.bumptech.glide:glide:4.12.0'
    //annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    //https://github.com/Blankj/AndroidUtilCode 许多工具
    api 'com.blankj:utilcodex:1.31.1'

    //https://github.com/square/okhttp
    api "com.squareup.okhttp3:okhttp:4.9.3"

    //https://github.com/getActivity/EasyHttp 轮子哥网络请求框架
    api 'com.github.getActivity:EasyHttp:12.8'

    // 腾讯 MMKV：https://github.com/Tencent/MMKV (EasyHttp缓存需要)
    api ('com.tencent:mmkv-static:1.3.2') {
        // 避免版本不一致导致的依赖冲突，从而导致编译报错
        exclude group: 'androidx.annotation', module: 'annotation'
    }

    //https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    //TODO 考虑使用: https://github.com/liangjingkanji/BRV
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'

    //https://github.com/getActivity/XXPermissions 轮子哥权限(Android 10需要适配分区存储特性)
    api 'com.github.getActivity:XXPermissions:18.6'

    // 吐司框架：https://github.com/getActivity/Toaster  轮子哥吐司
    api 'com.github.getActivity:Toaster:12.6'

    // Shape 框架：https://github.com/getActivity/ShapeView
    api 'com.github.getActivity:ShapeView:9.0'

    //https://github.com/li-xiaojun/XPopup 各种Dialog & Popup (compileSdkVersion 29)
    api 'com.github.li-xiaojun:XPopup:2.9.19'



    //GreenDao数据库
    compileOnly project(path: ':tempgen')

    //https://github.com/google/flexbox-layout
//    compileOnly 'com.google.android:flexbox:1.0.0'
    compileOnly 'com.google.android.flexbox:flexbox:3.0.0'

    //https://github.com/greenrobot/greenDAO 数据库
    compileOnly rootProject.ext.dependencies.greendao

    //https://github.com/yuweiguocn/GreenDaoUpgradeHelper greenDAO数据库升级
    compileOnly 'io.github.yuweiguocn:GreenDaoUpgradeHelper:v2.2.1'

    //https://github.com/square/retrofit 依赖配置:https://github.com/square/retrofit/blob/trunk/gradle/libs.versions.toml
    compileOnly 'com.squareup.retrofit2:retrofit:2.9.0'

    //https://github.com/square/retrofit/tree/master/retrofit-converters/gson (已经依赖了Gson2.8.5)
    compileOnly 'com.squareup.retrofit2:converter-gson:2.9.0'

    //https://github.com/hongyangAndroid/okhttputils 张鸿洋的okhttp, 55.9KB
    compileOnly ('com.zhy:okhttputils:2.6.2') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'//3.3.1
    }

    //https://github.com/JessYanCoding/ProgressManager Okhttp/Retofit/Glide下载进度监听
    compileOnly 'me.jessyan:progressmanager:1.5.0'

    //https://github.com/yellowcath/VideoProcessor 视频压缩
    compileOnly 'com.github.yellowcath:VideoProcessor:2.4.2'



    /**
     * TODO: 没注意是哪个库引起的, 当项目添加library依赖后, 会报错(报错其中1个), 以后移除
     * https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
     */
    constraints {
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.internal.jdk7.JDK7PlatformImplementations found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk7-1.5.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0)
         */
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk8-1.6.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0)
         */
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

configurations.all {
    resolutionStrategy {
        //如果 constraintlayout 2.0.4版本, 会有一些依赖包的androidx_core版本冲突, 这儿强制使用1.5.0
        force rootProject.ext.dependencies.androidx_core
    }
}